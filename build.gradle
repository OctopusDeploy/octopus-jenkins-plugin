plugins {
  alias(libs.plugins.jenkins)
  alias(libs.plugins.license)
  alias(libs.plugins.spotless)
}

group = 'hudson.plugins.octopusdeploy'
description = 'Octopus Deploy'

defaultTasks 'build', 'checkLicenses', 'jpi'

// Exclude license tasks provided by plugin (see custom tasks 'check-licenses.gradle')
project.gradle.startParameter.excludedTaskNames = [
  'licenseMain',
  'licenseTest',
  'licenseIntegrationTest'
]
apply from: "${rootDir}/gradle/check-licenses.gradle"

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
  implementation libs.apache.commons.collections4
  implementation libs.google.gson
  implementation libs.jenkins.workflow
  compileOnly libs.jetbrains.annotations

  testImplementation libs.bundles.junit
  testImplementation libs.assertj.core
  testImplementation libs.mockito.jupiter
  testImplementation libs.mockito.inline
  testImplementation libs.bundles.octopus
  testImplementation libs.apache.commons.text

  integrationTestImplementation libs.bundles.octopus
  integrationTestImplementation libs.bundles.junit
  integrationTestImplementation libs.assertj.core
  integrationTestImplementation libs.mockito.inline
  integrationTestImplementation libs.mockito.jupiter
  integrationTestImplementation libs.apache.commons.text
}

jenkinsPlugin {
  jenkinsVersion = '2.462.3'
  shortName = 'octopusdeploy'
  displayName = 'Octopus Deploy'
  url = 'https://github.com/OctopusDeploy/octopus-jenkins-plugin'
  gitHubUrl = 'https://github.com/OctopusDeploy/octopus-jenkins-plugin'
  compatibleSinceVersion = '1.612'
  fileExtension = 'hpi'
  configureRepositories = true

  developers {
    developer {
      id = 'octopusdeploy'
      name = 'Octopus Deploy'
      email = 'support@octopus.com'
    }
  }
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
  maven { url "https://packages.octopushq.com/artifactory/maven" }
}

tasks.withType(GenerateModuleMetadata) {
  enabled = false
}

spotless {
  groovyGradle {
    target '**/*.gradle'
    greclipse()
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
}

test {
  useJUnitPlatform()
  
  // Pass required environment variables to test JVM
  environment "OCTOPUS_SDK_AT_USE_EXISTING_SERVER", System.getenv("OCTOPUS_SDK_AT_USE_EXISTING_SERVER")
  environment "OCTOPUS_SERVER_URL", System.getenv("OCTOPUS_SERVER_URL")
  environment "OCTOPUS_SERVER_API_KEY", System.getenv("OCTOPUS_SERVER_API_KEY")
  environment "OCTOPUS_CLI_PATH", System.getenv("OCTOPUS_CLI_PATH")
  environment "OCTOPUS_LICENSE", System.getenv("OCTOPUS_LICENSE")
  
  // Debug: Print environment variable status (not values to avoid leaking secrets)
  doFirst {
    println "=== Test Environment Variables Status ==="
    println "OCTOPUS_SERVER_URL: ${environment['OCTOPUS_SERVER_URL'] ? 'SET (length: ' + environment['OCTOPUS_SERVER_URL'].length() + ')' : 'NOT SET'}"
    println "OCTOPUS_CLI_PATH: ${environment['OCTOPUS_CLI_PATH'] ? 'SET (length: ' + environment['OCTOPUS_CLI_PATH'].length() + ')' : 'NOT SET'}"
    println "OCTOPUS_SDK_AT_USE_EXISTING_SERVER: ${environment['OCTOPUS_SDK_AT_USE_EXISTING_SERVER'] ? 'SET (value: ' + environment['OCTOPUS_SDK_AT_USE_EXISTING_SERVER'] + ')' : 'NOT SET'}"
    println "OCTOPUS_SERVER_API_KEY: ${environment['OCTOPUS_SERVER_API_KEY'] ? 'SET (length: ' + environment['OCTOPUS_SERVER_API_KEY'].length() + ')' : 'NOT SET'}"
    println "OCTOPUS_LICENSE: ${environment['OCTOPUS_LICENSE'] ? 'SET (length: ' + environment['OCTOPUS_LICENSE'].length() + ')' : 'NOT SET'}"
    println "=========================================="
  }
}

if (file('src/integration-test').directory) {
  sourceSets {
    integrationTest {
      java {
        compileClasspath += main.output
        runtimeClasspath += main.output
        srcDir file('src/integration-test/java')
      }
      resources.srcDir file('src/integration-test/resources')
    }
  }

  task integrationTest(type: Test, dependsOn: ["compileTestJava"]) {
    useJUnitPlatform()
    maxParallelForks = 1 // Consider increasing this when test volume exceeds overhead of docker containers starting
    group = "verification"
    description = "Runs integration tests"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
  }
}
